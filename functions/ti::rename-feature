#!/usr/bin/env zsh
#
# Copyright (c) 2022 Sebastian Gniazdowski
#
0=${(%):-%x}
: ${TIG_SUITE_DIR:=$0:h:h} ${Plugins[TIG_DIR]:=$0:h:h}

local qmet=$1 qi=$3 qn=$4 q
local -aU cont=(${(@f)"$(<$TIREGI_FILE)"}) qa
integer qc

# Open fuzzy finder if no feature ID supplied
if ((!$#qi||!$#qn))||[[ $qmet == tui ]];then
    ti::util-verify-chooser-app||return 1
    cat $TIREGI_FILE|sed -r -e 's/ Q:"[^"]+"//g'|sort -n|\
    {
        print ${(r:COLUMNS::—:):-—}
        print PICK A °FEATURE° TO RENAME…
        print Below are the configured °features°, click to pick one.
        print ${(r:COLUMNS::—:):-—}
        print "${(r:3:: :):-}"PID/FeatID"${(r:25:: :):-}"Files
        print ${(r:COLUMNS::—:):-—}
        cat
    }|{
        q="$($TIFZF_BIN)"
        ret+=$?
        qi=${${q/(#b)(#s)$PID\/([^[:space:]]##)*(#e)/${match[1]}}%:}
        if [[ -n $qi ]]; then
            () {
                autoload -Uz colors; colors
                local IFS=
                builtin read </dev/tty >/dev/tty  \
                    qn\?"New ID of the now ID=<$fg_bold[green]$qi$reset_color> °feature°: $fg_bold[magenta]"
                if (($#qn<3));then
                     ((!$#qn))&&timsg %B{208}Error:%b{137} New ID not \
                            specified, exiting…
                     (($#qn))&&timsg %B{208}Error:%b{137} Length of ID has \
                            to be at least %B{33}3%b{137}, exiting…
                    return ret+1
                fi
            };(($?))&&return $?
        fi
    }
fi

if [[ -n $qi && -n $qn ]];then
    local -aU prev=(${(M)cont:#(#s)$PID/$qi: *}) filtered=(${cont:#(#s)$PID/$qi: *})
    qa=(${prev[@]/(#s)(#b)($PID\/$qi):/${$((qc+=1)):+}$PID/$qn:})
    ((!qc))&&{
        timsg -h %B{208}Error:%f%b The substitution has failed, \
            no change done to the {41}registry-file
        return ret+1-qc
    }
    ((qc>1))&&{
        timsg -h %B{208}Warning:%f%b number of matches \>%B{208}1%f%b
        ret+=qc-1
    }
    repeat 1 {printf %s\\n $filtered>!$TIREGI_FILE;ret+=$?;}
    repeat 1 {printf %s\\n $qa>>!$TIREGI_FILE;ret+=$?;}
    timsg %B{27}Feature: {41}$qi{27} renamed to: {41}$qn
    ((ret))&&timsg -h %B{208}warning:%f%b non-zero exit code \
        \<%B{205}$ret%b%f\> from some of operations
else
        timsg %B{208}Error:%f%b Two IDs required, {41}from%f and {33}to%f,\
            exiting… 
fi



