#!/usr/bin/env zsh
# Possibly fix $0 with a new trick – use of a %x prompt expansion
0="${${(M)${0::=${(%):-%x}}:#/*}:-$PWD/$0}"
: ${TIG_SUITE_DIR:=$0:h:h}

# Input/options
local -A Opts
builtin zparseopts -D -E -A Opts -- -exec || return 1

util/ti::verify-tigsuite-dir||return 1
util/ti::verify-chooser-app||return 1

local qc=.tig_history qi
local -a CFG=( $HOME/$qc(N) $XDG_CONFIG_HOME/tig/$qc(N) $HOME/.config/tig/$qc(N) )
CFG=$CFG[1]
local -a tasks=(${(M)${(@f)"$(<$CFG)"}:#(\!|exec[[:space:]]##)*})
integer ret

{
    print ${(r:COLUMNS::–:):-–}
    print Tig saved pager-view commands:
    print ${(r:COLUMNS::–:):-–}
    printf ·%s\\n ${(Oa)tasks}
}|{qi="$($TICHOOSE_APP)";ret+=$?;}

local qtmp=$TICACHE/tig-script
cat >$qtmp <<end
:bind generic ex :echo "In a new \`tig\` from previous \`ex\` command, quit first"
:${qi#·}
end

#(($+Opts[--exec]))&&TIG_SCRIPT=$qtmp exec tig
((!$+Opts[--exec]))&&TIG_SCRIPT=$qtmp tig
