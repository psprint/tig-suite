#!/usr/bin/env zsh
0="${(%):-%x}"
0="${${(M)0:#/*}:-$PWD/$0}"
: ${TIG_SUITE_DIR:=$0:h:h} ${Plugins[TIG_DIR]:=$0:h:h}

# Input/options
local -A Opts
builtin zparseopts -D -E -A Opts -- || return 1

local FROM=$1 CID=$2 ID OP
OP=copy-$FROM


# Initialize
util/ti::verify-cid "$FROM" $CID||return 1
util/ti::tigsuite-startup $OP "" $FROM-$CID||return 1

# Read the commit or feature 
if [[ $FROM == commit ]];then
    {reply=(${(@0)"$(git diff -z --name-only $CID~..$CID)"});} TIO
    # Try again with $PDIR passed as repo dir
    ((!$#reply))&&\
        {reply=(${(@0)"$(git -C $PDIR diff -z --name-only $CID~..$CID)"});} TIO
fi
if [[ $FROM == feature ]];then
    feat/ti::open-feature --get-files-only||return 1
    ID=${REPLY%%$'\0'*} REPLY=${REPLY##*$'\0'} 
    if [[ $REPLY != $PDIR ]];then
        tigmsg %B{208}Error:{138}%b chosen °{27}feature{208}≈{39}$ID\
{138}° not from PWD, cannot copy files, exitinig…
        return 1
    fi
fi
if [[ $FROM == tree ]];then
    reply=(${(@f)"$(command git ls-files|grep -vE '(README.(md|txt)|LICENSE|\.gitignore)')"})
fi
if [[ $FROM == tree-of-commit ]];then
    util/ti::verify-cid commit $CID||return 1
    reply=(${(@f)"$(command git ls-tree -r --name-only $CID|command grep -vE '(README.(md|txt)|LICENSE|\.gitignore)')"})
fi

local -a qa=($reply)

# Create accu
[[ ! -f $TIPID_QUEUE ]]&&builtin print -n>!$TIPID_QUEUE 2>$TINL
integer ret

repeat 1 {builtin printf %s\\n $qa>>!$TIPID_QUEUE;ret+=$?;}

if ((!ret));then
    tigmsg %B{208}Notice:{138} queue of project {39}${(U)PID}{138} \
        extended with files from $FROM
else
    tigmsg -h %B{208}Warning:{138}%b non-zero code (%B{41}$ret%f%b),
        something went wrong
fi
