#!/usr/bin/env zsh
0="${(%):-%x}"
0="${${(M)0:#/*}:-$PWD/$0}"
: ${TIG_SUITE_DIR:=$0:h:h} ${Plugins[TIG_DIR]:=$0:h:h}

local TMP
typeset -g TICHOOSE_APP

# configure fuzzy searcher
[[ ! -x =$TICHOOSE_APP ]]&&for TMP in tig-pick fzf fzy; do
    (($+commands[$TMP]))&&TICHOOSE_APP=$TMP&&break
    [[ -x $TIG_SUITE_DIR/bin/$TMP ]]&&TICHOOSE_APP=$TIG_SUITE_DIR/bin/$TMP&&break
done
REPLY=$TICHOOSE_APP

if [[ ! -x =$TICHOOSE_APP ]];then
    local -Ua q=($TMP tig-pick fzf fzy)
    tigmsg -h %B{204}Error:%f%b no chooser app found, tried one of:\\n\
%B{39}·${(~j.%f%b, {39}%B·.)q}%f%b,\\nand currently \
${${TICHOOSE_APP:+set %B{41}TICHOOSE_APP%f%b to: %B{39}$TICHOOSE_APP%f%b}:-\
%B{41}TICHOOSE_APP%f%b is unset)}
    return 1
fi

return 0