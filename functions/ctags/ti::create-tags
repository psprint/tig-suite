#!/usr/bin/env zsh
# Copyright (c) 2023 Sebastian Gniazdowski

# Possibly fix $0 with a new trick – use of a %x prompt expansion
0="${${(M)${0::=${(%):-%x}}:#/*}:-$PWD/$0}"
: ${TIG_SUITE_DIR:=$0:h:h}

local tpe=$1 opt=${${2:#tags}:+-e} fmt=$2
integer ec=-1

# C?
if [[ $tpe == (#i)c ]];then
    repeat 1 {ctags $opt -R --languages=C --langmap=C:.h.c --c-kinds=+px --extras=+r --extras=-{anonymous};ec+=1+$?;}
fi

# Zsh?
if [[ $tpe == (#i)zsh ]];then
    repeat 1 {ctags -R $opt -G --maxdepth=3 \
        --options=$TIG_SUITE_DIR/share/zsh.ctags --languages=zsh3,zsh,sh;ec+=1+$?;}
fi

# Unknown language requested?
if ((ec<0));then
    tigmsg -h %B{208}Error:{137} unknown language {41}$tpe{137}, exiting…
    return 1
fi

# No error?
if ((ec==0));then
    tigmsg %B{41}TAGS{39} file created without errors. %f%bIt has{39} \
        ${${(M)$(grep -Ec '^[[:alnum:]]+' $fmt)##<->##}// ##/ }%f symbols.
fi

# Exit code > 0?
if ((ec>0));then
    tigmsg -h %B{208}Error:{137} Non 0-exit code ({208}$ec{137}), exiting…
    return 1
fi

return ec
