#!/usr/bin/env zsh
# Copyright (c) 2023 Sebastian Gniazdowski

# Possibly fix $0 with a new trick – use of a %x prompt expansion
0="${${(M)${0::=${(%):-%x}}:#/*}:-$PWD/$0}"

emulate zsh -o extended_glob
typeset -g LOG_DIR=${XDG_CACHE_HOME:-$HOME/.cache}/tig

function log()
{
        local open=$1 l=${(M)2##<->##} time=`date +%y%m%d-%T`
        mkdir -p $LOG_DIR
        print
        if [[ -z $l ]]; then
            print -Pr -- %B%F{174}'\\'%F{69}°%F{174}'\\' %F{40}Opening%F{69}: %F{27}$open%f%b on $time | tee -a $LOG_DIR/open.log
        else
            print -Pr -- %B%F{174}'\\'%F{69}$l%F{174}'\\' %F{40}Opening%F{69}: %F{27}$open%F{14}:%F{174}$l%f%b on $time | tee -a $LOG_DIR/open.log
        fi
        sleep 1
}

function find-matches()
{
     local f=$1
     { reply=( **/$f:t(.NDY1) ) } 2>/dev/null
     return $(( !$#reply ))
}

msg()
{
    print -rP -- "%F{208}%BFailed to find the matched filename part (last used was: %F{40}$1%F{208})%f%b" \
        | tee -a $LOG_DIR/open.log
    (( $2 )) && exit $2
}

edit()
{
    local que=$EDITOR
    (($+commands[$que]))||que=mcedit
    (($+commands[$que]))||que=emacs
    (($+commands[$que]))||que=micro
    (($+commands[$que]))||que=vim
    (($+commands[$que]))||{print -P -- %B%F{135}Couldn\'t find editor, please \
        set {208}\$EDITOR{135}, exiting…;return 1;}
 
    command "$que" ${${(M)2##(#m)<->##}:+$MATCH} $1
}

if [[ $1 == (#b)*(([[:space:]]##)|(#s))([^[:space:]]##):(#B)(:(#b)([^[:space:]]##)|)([[:space:]]##|(#e)) || $1 == (#b)[[:space:]]#((([^:]##))):([0-9]##|):* ]]; then
    find-matches $match[3]:t || { msg $match[3]:t 1;}
    log "$reply[1]" $match[4]
    edit "$reply[1]" $match[4]
else
    # Multiple tries – one for each token
    { local -a arr=( ${(Z+Cn+@)1} ) } 2>/dev/null
    local f
    for f in $arr; do
        if [[ -f $f ]]; then
            reply=( $f )
            log "$f"
            edit "$f"
        elif [[ $f == ([^\$/\\:=+\#[:space:]]##|(#s))(#b)[[:space:]]#([^/]#(/(([^/]##/)#[^/\"[:space:]]#)))* ]]; then
            eval "match[1]=\"${match[1]//\"/\\\"}\""

            if [[ -f $match[1] ]];then
                log "$match[1]"; edit "$match[1]"
            elif [[ -f /$match[3] ]];then
                log "/$match[3]"; edit "/$match[3]"
            elif [[ -f $match[3] ]];then
                log "$match[3]"; edit "$match[3]"
            fi
            reply+=($_)
        elif [[ $f == (#b)*(([[:space:]]##)|(#s))([^:[:space:]]##)(#B)(:(#b)([^[:space:]]##)|)([[:space:]]##|(#e))* ]]; then
            if [[ -f $match[3] ]]; then
                reply=( $match[3] )
                log "$match[3]" $match[4]
                edit "$match[3]" $match[4];
            else
                find-matches $match[3]:t || { continue}
                log "$reply[1]" $match[4]
                edit "$reply[1]" $match[4];
            fi
        fi
        (($#reply))&&break
    done
fi

(( $#reply )) || msg "$match[3]:t" 1
